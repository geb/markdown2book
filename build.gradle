apply plugin: "groovy"
apply plugin: "maven"
apply plugin: "idea"
apply from: "$rootDir/gradle/idea.gradle"
apply plugin: "signing"

archivesBaseName = "markdown2book"
description = "The documentation system used for the Book of Geb"
group = "org.gebish"
version = "1.0"
ext.isSnapshot = version.endsWith("-SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    groovy \
        "org.codehaus.groovy:groovy-all:2.0.6"

    compile \
        "commons-io:commons-io:1.4",
        "org.pegdown:pegdown:1.1.0"

    testCompile \
        "org.spockframework:spock-core:0.7-groovy-2.0"
}

task sourcesJar(type: Jar, dependsOn: classes) { 
    classifier = 'sources' 
    from sourceSets.main.allSource
} 

task javadocJar(type: Jar, dependsOn: groovydoc) { 
    classifier = 'javadoc' 
    from groovydoc.destinationDir 
} 

signing {
    sign configurations.archives
    required { !isSnapshot && gradle.taskGraph.hasTask(uploadArchives) }
}

artifacts {
    archives javadocJar, sourcesJar
}

apply from: "$rootDir/gradle/pom.gradle"

uploadArchives { task ->
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        gradle.taskGraph.whenReady { taskGraph ->
            if (taskGraph.hasTask(task)) {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeOssUsername, password: sonatypeOssPassword)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatypeOssUsername, password: sonatypeOssPassword)
                }
            }
        }
    }
}

modifyPom {
    project {
        url "http://www.gebish.org"
        delegate.description description
        inceptionYear "2010"
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        scm {
            url "https://github.com/geb/markdown2book"
        }
        developers {
            developer {
                id "ldaley"
                name "Luke Daley"
                roles {
                    role "Lead"
                    role "Founder"
                }
            }
        }
    }
    
    dependencies.removeAll(dependencies.findAll { it.scope == "test" })
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.3"
}